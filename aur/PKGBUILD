# Maintainer: Bouteiller a2n Alan <hi@a2n.dev>
pkgname=conar-bin
pkgver=0.26.1
pkgrel=1
pkgdesc="AI-powered tool for working with Postgres, MySQL, MSSQL and ClickHouse."
arch=('x86_64')
url="https://github.com/wannabespace/conar"
license=('AGPL-3.0-or-later')
depends=(
  'alsa-lib'
  'at-spi2-core'
  'cairo'
  'dbus'
  'expat'
  'gcc-libs'
  'glib2'
  'glibc'
  'gtk3'
  'hicolor-icon-theme'
  'libcups'
  'libx11'
  'libxcb'
  'libxcomposite'
  'libxdamage'
  'libxext'
  'libxfixes'
  'libxkbcommon'
  'libxrandr'
  'mesa'
  'nspr'
  'nss'
  'pango'
  'systemd-libs'
)
options=('!strip' '!emptydirs')
source_x86_64=("conar-${pkgver}.deb::https://download.conar.app/linux/deb/x64")
sha256sums_x86_64=('SKIP')

package() {
  # Extract the .deb file
  ar -x "${srcdir}/conar-${pkgver}.deb"

  # Extract the data archive
  tar -xJ -f data.tar.xz -C "${pkgdir}"

  # Create symlink for command line usage
  install -dm755 "${pkgdir}/usr/bin"
  ln -s "/opt/Conar/Conar" "${pkgdir}/usr/bin/conar"

  # Fix desktop file path if it exists
  if [ -f "${pkgdir}/usr/share/applications/conar.desktop" ]; then
    sed -i 's|/opt/Conar/Conar|conar|g' "${pkgdir}/usr/share/applications/conar.desktop"
  fi
}
